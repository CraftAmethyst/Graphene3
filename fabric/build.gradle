plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'com.gradleup.shadow' version '9.0.2'
}

repositories {
    maven {
        url = 'https://api.modrinth.com/maven'
        content { includeGroup 'maven.modrinth' }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    // Compile common sources directly, plus match its runtime deps needed by those sources
    implementation project(":common")

    //Create
    implementation 'maven.modrinth:create:8amzvn9x'

    //TR
    implementation "maven.modrinth:tritiumconfiguration:${tc_fabric}"

    //AE2
    modImplementation 'teamreborn:energy:3.0.0'
    modImplementation 'maven.modrinth:ae2:kA3rm9EP'
    modImplementation 'maven.modrinth:applied-energistics-2-wireless-terminals:QewbGM3G'

    //include 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    //include 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    implementation 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'

    // Config UI + AutoConfig (Fabric)
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"

    // ModMenu integration
    modImplementation "maven.modrinth:modmenu:lEkperf6"// 7.2.2 VERSION

   shadow 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}
configurations {
    shadow {
        canBeResolved = true
        canBeConsumed = false
    }
}
tasks.remapJar {
    dependsOn shadowJar
    mustRunAfter(tasks.shadowJar)
    setInput(tasks.shadowJar.outputs.files[0])
}
tasks.shadowJar {
    archiveClassifier = 'all' //DON NOT MODIFY IT,OTHERWISE THE REOBF JAR TASK WILL CAN'T FOUND THE FILE
    enableAutoRelocation = true
    relocationPrefix = "${mod_id}.libs"
    configurations = [project.configurations.shadow]
    exclude 'META-INF/maven/**'
}
