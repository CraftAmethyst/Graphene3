plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

version = version
group = group

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":Common")

    // Core libs for culling features
    implementation 'org.joml:joml:1.10.5'
    include 'org.joml:joml:1.10.5'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    include 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    implementation 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'
    include 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'

    // Config UI + AutoConfig (Fabric)
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"

    // Mod Menu for config screen integration
    modImplementation "com.terraformersmc:modmenu:4.0.6"
}

loom {
    mixin {
        defaultRefmapName = "tritium.refmap.json"
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
