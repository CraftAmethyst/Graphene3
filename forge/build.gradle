plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'java'
    id 'com.gradleup.shadow' version '9.0.2'
}
base {
    archivesName = "${mod_name}-forge-${minecraft_version}"
}

repositories {
    mavenLocal()
    maven { url = uri("build/local-repo") }
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/releases' }
    maven { url = "https://maven.shedaniel.me/" }

    maven {
        name = 'CurseMaven'
        url = 'https://cursemaven.com'
        content { includeGroup 'curse.maven' }
    }

    maven {
        url = 'https://api.modrinth.com/maven'
        content { includeGroup 'maven.modrinth' }
    }

    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }

    maven { url = 'https://jitpack.io' }
    maven {
        name = 'CodeMC-OcclusionCulling'
        url = 'https://ci.codemc.io/job/LogisticsCraft/job/OcclusionCulling/lastSuccessfulBuild/artifact/'
    }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    mavenCentral()
    maven { name = 'Shedaniel'; url = 'https://maven.shedaniel.me/' }
    maven { name = 'TerraformersMC'; url = 'https://maven.terraformersmc.com/releases/' }
}
mixin {
    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}
minecraft {
    mappings channel: 'official', version: minecraft_version

    copyIdeResources = true //Calls processResources when in dev

    reobf = false // Forge 1.20.6+ uses official mappings at runtime, so we shouldn't reobf from official to SRG

    // Automatically enable forge AccessTransformers if the file exists
    // This location is hardcoded in Forge and can not be changed.
    // https://github.com/MinecraftForge/MinecraftForge/blob/be1698bb1554f9c8fa2f58e32b9ab70bc4385e60/fmlloader/src/main/java/net/minecraftforge/fml/loading/moddiscovery/ModFile.java#L123
    // Forge still uses SRG names during compile time, so we cannot use the common AT's
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformer = at
    }

    runs {
        client {
            workingDirectory file('runs/client')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory file('runs/server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory file('runs/data')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            mods {
                modDataRun {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.5"))
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
    //AE2WTLib
    implementation 'maven.modrinth:applied-energistics-2-wireless-terminals:IQFaf9vp'
    //Create
    implementation 'maven.modrinth:create:88L641Un'
    // Config UI + AutoConfig
    implementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    shadow 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    shadow 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'
    implementation "maven.modrinth:tritiumconfiguration:${tc}"
    // Forge's hack fix
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            fg.component(it)
        }
    }
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
configurations {
    shadow {
        canBeResolved = true
        canBeConsumed = false
    }
}

tasks.shadowJar {
    archiveClassifier = ''
    enableAutoRelocation = true
    relocationPrefix = 'org.craftamethyst.tritium.libs'

    configurations = [project.configurations.shadow]
}