plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id 'com.gradleup.shadow' version '9.0.2'
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        url = 'https://api.modrinth.com/maven'
        content { includeGroup 'maven.modrinth' }
    }
}

dependencies {
    // Compile common sources directly; repeat its runtime deps here for compilation
    compileOnly project(":common")

    //Create
    implementation 'maven.modrinth:create:8amzvn9x'

    //TR
    implementation "maven.modrinth:tritiumconfiguration:${tc}"

    //AE2
    implementation 'maven.modrinth:guideme:9YGnKYDF'
    implementation 'maven.modrinth:ae2:7KVs6HMQ'
    implementation 'maven.modrinth:curios:IPQlZkz1'
    implementation 'maven.modrinth:architectury-api:1MKTLiiG'
    implementation 'maven.modrinth:applied-energistics-2-wireless-terminals:BLQZoB0F'

    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'
    implementation 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'
    implementation 'org.jetbrains:annotations:24.1.0'

    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")

    implementation group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    // Config UI + AutoConfig (Forge)
    implementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"

    // Bundle Caffeine and dependencies into the JAR
    shadow 'com.logisticscraft:occlusionculling:0.0.8-SNAPSHOT'
    shadow 'io.github.llamalad7:mixinextras-common:0.3.5'

}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}

configurations {
    shadow {
        canBeResolved = true
        canBeConsumed = false
    }
}

tasks.reobfJar{
    dependsOn shadowJar
    mustRunAfter(tasks.shadowJar)
    setInput(tasks.shadowJar.outputs.files[0])
}

tasks.shadowJar {
    File refmapFile;
    for (final def res in sourceSets.main.resources.files) {
        if(res.name.endsWith("${mod_id}.forge.mixins.json")){
            refmapFile = res;
            println "[ShadowJar] Found the mixin json: "+res
        }
    }
    if(refmapFile!=null){
        java.nio.file.Path path = refmapFile.parentFile.parentFile.parentFile.parentFile.toPath().resolve("build").resolve("mixin").resolve("${mod_id}.refmap.json")
        println "[ShadowJar] Adding " + path + " into the shadow jar: "+ outputs.files[0]
        refmapFile = path.toFile()
        from refmapFile
    }
    archiveClassifier = 'all' //DON NOT MODIFY IT,OTHERWISE THE REOBF JAR TASK WILL CAN'T FOUND THE FILE
    enableAutoRelocation = true
    relocationPrefix = "org.craftamethyst.${mod_id}.libs"
    configurations = [project.configurations.shadow]
    exclude 'META-INF/maven/**'
}

tasks.named('jar') {
    from sourceSets.main.output
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}
tasks.build.dependsOn tasks.shadowJar, tasks.reobfJar
tasks.assemble.dependsOn tasks.shadowJar, tasks.reobfJar