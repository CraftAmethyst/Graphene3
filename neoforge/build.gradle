plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

repositories {
    mavenCentral()
    maven { name = 'Shedaniel'; url = 'https://maven.shedaniel.me/' }
    maven { name = 'TerraformersMC'; url = 'https://maven.terraformersmc.com/releases/' }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    // Config UI + AutoConfig (NeoForge)
    implementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
